include(FindPackageHandleStandardArgs)

find_package(PkgConfig QUIET)
if (PKG_CONFIG_FOUND)
  pkg_check_modules(SAMPLERATE samplerate QUIET)
endif ()

if ( NOT SAMPLERATE_FOUND )
  find_path(SAMPLERATE_INCLUDE_DIRS NAMES samplerate.h)
  find_library(SAMPLERATE_LIBRARIES NAMES samplerate)

  if ( NOT "${SAMPLERATE_LIBRARIES}" STREQUAL "")
    set (SAMPLERATE_FOUND TRUE)
    set (SAMPLERATE_LINK_LIBRARIES ${SAMPLERATE_LINK_LIBRARIES} ${SAMPLERATE_LIBRARIES})
  endif ()
endif ()

if ( SAMPLERATE_INCLUDEDIR AND NOT SAMPLERATE_INCLUDE_DIRS )
  set (SAMPLERATE_INCLUDE_DIRS ${SAMPLERATE_INCLUDEDIR})
endif ()
  
find_package_handle_standard_args(SampleRate
  FOUND_VAR
    SAMPLERATE_FOUND
  REQUIRED_VARS
    SAMPLERATE_LINK_LIBRARIES
    SAMPLERATE_INCLUDE_DIRS
  VERSION_VAR
    SAMPLERATE_VERSION)

if ( SAMPLERATE_FOUND AND NOT TARGET SampleRate::samplerate )
  add_library(SampleRate::samplerate INTERFACE IMPORTED GLOBAL)
  set_target_properties(SampleRate::samplerate
    PROPERTIES
      VERSION "${SAMPLERATE_VERSION}"
      LOCATION "${SAMPLERATE_LINK_LIBRARIES}"
      INTERFACE_INCLUDE_DIRECTORIES "${SAMPLERATE_INCLUDE_DIRS}"
      INTERFACE_LINK_LIBRARIES "${SAMPLERATE_LINK_LIBRARIES}")
endif ()
